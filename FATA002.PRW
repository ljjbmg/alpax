/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFATA002   บAutor  ณWagner Gomes        บ Data ณ  28/06/04   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Funcao com Browse sobre as Notas Fiscais de Saida          บฑฑ
ฑฑบ          ณ permitido sejam digitados seus dados complementares        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Especifico Alpax                                           บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

#INCLUDE "rwmake.ch"           
#INCLUDE "Topconn.ch"

User Function FatA002()


Private cCadastro := "Dados Complementars da Notas Fiscais de Saida"
Private _nValor	:= 0
Private aRotina 	:= { 	{"Pesquisar"	,"AxPesqui"		,0,1		} 	,;
		             		{"Visualizar"	,"MC090Visual"	,0,2		} 	,;
             				{"Atualizar"	,"U__fGera()"	,0,3		} 	,;
             				{"Legenda"		,"U__fLeg()"	,0,8		} 	}
Private cString 	:= "SF2"

aCores := 	{	{ " Empty(F2_ESPECI1) .AND.  Empty(F2_AXIMPR)"	, 'BR_VERDE' 	}	,;
            	{ "!Empty(F2_ESPECI1) .AND.  Empty(F2_AXIMPR)" 	, 'BR_AZUL' 	}   ,;
            	{ "!Empty(F2_AXIMPR)"							, 'BR_VERMELHO'	} }

dbSelectArea(cString)
dbSetOrder(1)                                 
//Set Filter to E1_SALDO <> 0
DbGoTop()

mBrowse( ,,,,cString,,,,,,aCores)

DbSelectArea(cString)
//Set Filter To

Return                           


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ_fLeg     บAutor  ณWagner Gomes        บ Data ณ  30/09/03   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFUNCAO DE LEGENDA.                                          บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Especifico Alpax                                           บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

User Function _fLeg()

Local aCores
Local cCadastro

cCadastro := "Dados Complementares das Notas Fiscais de Saida"
aCores    := {	{ 'BR_VERDE'	, "Permite Digitacao" 	}	,;
				{ 'BR_AZUL'		, "Permite Alteracao"	}	,;
				{ 'BR_VERMELHO'	, "Nota Ja Emitida"		}	}

BrwLegenda(cCadastro,"Legenda do Browse",aCores)

Return(.t.)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ_fGera    บAutor  ณWagner Gomes        บ Data ณ  28/06/04   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณFuncao para digitacao dos valores e confirmacao             บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Especifico Alpax                                           บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

User Function _fGera()

If ! fValidQtd()
	Return
EndIf

DbSelectArea("SF2")

If !Empty(F2_AXIMPR)
	Return
EndIf
_cDoc		:= F2_DOC+"/"+F2_SERIE

_cCliente	:= F2_CLIENTE
_cLoja		:= F2_LOJA
_cRazao		:= GetAdvfVal("SA1","A1_NOME",xFilial("SA1")+F2_CLIENTE+F2_LOJA,1,"")

_nPLq		:= F2_PLIQUI
_nPbr		:= F2_PBRUTO
_cBox		:= F2_AXBOX
_nVol		:= F2_VOLUME1
_cTpVol		:= F2_ESPECI1

//ฺฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฟ
//ณ Criacao da Interface                                                ณ
//ภฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤฤู
@ 138,047 To 463,913 Dialog oCompl Title OemToAnsi("Dados Complementares da Nota Fiscal")
@ 020,009 To 065,404 Title OemToAnsi("Nota Fiscal e Dados do Cliente")
@ 075,010 To 150,330 Title OemToAnsi("Dados Complementares da Nota Fiscal")

@ 008,300 Say OemToAnsi("Nota Fiscal e Serie : ") Size 60,8

@ 030,075 Say OemToAnsi("Cliente") Size 30,7
@ 030,125 Say OemToAnsi("Loja") Size 15,7
@ 030,150 Say OemToAnsi("Razao Social") Size 50,7

@ 085,020 Say OemToAnsi("Peso Bruto") Size 50,7
@ 085,100 Say OemToAnsi("Peso Liquido") Size 50,7
@ 085,170 Say OemToAnsi("Box") Size 30,7

@ 115,020 Say OemToAnsi("Quant de Volumes") Size 50,7
@ 115,100 Say OemToAnsi("Tipos de Volumes") Size 50,7

@ 007,365 Get _cDoc Size 40,10 When .f.

@ 045,075 Get _cCliente Size 40,10 When .f.
@ 045,125 Get _cLoja Size 15,10 When .f.
@ 045,150 Get _cRazao Size 250,10 When .f.

@ 100,020 Get _nPbr Picture "@E 99,999.999 " Size 50,10 Valid Positivo()
@ 100,100 Get _nPLq Picture "@E 99,999.999 " Size 50,10 Valid Positivo()
@ 100,170 Get _cBox Size 150,10 Valid !(Vazio())

@ 130,020 Get _nVol Picture "@E 99,999 " Size 50,10 Valid Positivo()
@ 130,100 Get _cTpVol Size 220,10 Valid !(Vazio())

@ 090,360 BmpButton Type 1 Action Processa( {|| _fGrava(), Close(oCompl) } )
@ 120,360 BmpButton Type 2 Action Close(oCompl)


Activate Dialog oCompl Centered

Return
/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ_fGrava   บAutor  ณWagner Gomes        บ Data ณ  28/06/04   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Funcao de gravacao dos dados complementares                บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ Especifico Alpax                                           บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function _fGrava()

//
// Inicio da transacao
//
Begin Transaction

dbSelectArea("SF2")
RecLock("SF2",.F.)
	F2_VOLUME1	:= _nVol
	F2_ESPECI1	:= Upper(_cTpVol)
	F2_PLIQUI	:= _nPLq
	F2_PBRUTO	:= _nPBr
	F2_AXBOX	:= Upper(_cBox)
	F2_AXDTEMB	:= dDataBase
MsUnLock()

End Transaction
//
// Fim da transacao
//

Return


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณfValidQtd บAutor  ณAdriano Luis Brandaoบ Data ณ  28/07/07   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Funcao para validacao da quantidade de itens na nota fiscalบฑฑ
ฑฑบ          ณ de saida.                                                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP - ALPAX.                                                บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function fValidQtd()

lRet := .f.      

_nItem := 0

@ 138,047 To 363,500 Dialog oDlg1 Title OemToAnsi("Dados Complementares da Nota Fiscal")

@ 020,009 To 065,200 Title OemToAnsi("Dados Nota Fiscal Serie:" + SF2->F2_SERIE + " / Nr." + SF2->F2_DOC  )

@ 040,0040 Say OemToAnsi("Numero de Itens da Nota Fiscal ") Size 60,8
@ 040,0110 Get _nItem Picture "@e 999" Size 030,10 

@ 075,100 BmpButton Type 1 Action ( lRet := iif(fValida(_nItem),.t.,.f.),Close(oDlg1) )
@ 075,150 BmpButton Type 2 Action Close(oDlg1)

Activate Dialog oDlg1 Centered

Return(lRet)


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณFATA002   บAutor  ณAdriano Luis Brandaoบ Data ณ  28/07/07   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Query buscando a quantidade de itens da nota fiscal.       บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ AP - ALPAX.                                                บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/
Static Function fValida(_nQuant)

Local _lValid := .t.

_cQuery := "SELECT COUNT(*) AS TOTAL "
_cQuery += "       FROM " + RetSqlName("SD2") + " D2 "
_cQuery += "       WHERE D2_FILIAL = '" + xFilial("SD2") + "' AND D2_SERIE = '" + SF2->F2_SERIE + "' "
_cQuery += "             AND D2_DOC = '" + SF2->F2_DOC + "' AND D_E_L_E_T_ = ' ' "

TcQuery _cQuery New Alias "QR2"

_nItens := QR2->TOTAL

QR2->(DbCloseArea())

If _nItens <> _nQuant
	ApMsgInfo("Quantidade de itens nesta nota fiscal invalida, confirmar quantidade !!!","AVISO")
	_lValid := .f.
EndIf	

Return(_lValid)